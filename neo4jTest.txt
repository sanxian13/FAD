//载入图数据，有表头
LOAD CSV WITH HEADERS FROM 'file:///edges.csv' AS row
WITH toInteger(row.id1) AS src, toInteger(row.id2) AS dst
MERGE (u:Node {id: src})
MERGE (v:Node {id: dst})
MERGE (u)-[:LINK]->(v);

//显示整个视图
MATCH (n:Node)
OPTIONAL MATCH (n)-[r:LINK]->(m:Node)
RETURN n, r, m;


//创建节点属性并标记每个节点的所在的k_l的属性，
LOAD CSV WITH HEADERS FROM 'file:///coreness.csv' AS row
WITH row
WHERE row.k IS NOT NULL AND row.l IS NOT NULL
MERGE (n:Node {id: toInteger(row.id)})
SET n.kl = coalesce(n.kl, []) + [trim(toString(row.k)) + "_" + trim(toString(row.l))];




//查询图中满足(1,4)-core的节点
MATCH (n:Node)-[r:LINK]->(m:Node)
WHERE "1_4" IN n.kl AND "1_4" IN m.kl
RETURN n, r, m;


---------------------------

LOAD CSV WITH HEADERS FROM 'file:///AF.csv' AS row
WITH row WHERE row.type IN ['anchor','follower'] AND row.id_list IS NOT NULL

WITH collect(CASE WHEN row.type = 'anchor' THEN row.id_list END)[0]           AS anchorId,
     collect(CASE WHEN row.type = 'follower' THEN row.id_list END)[0]         AS followerList
WITH toInteger(anchorId) AS anchorId, split(followerList, ' ') AS followers

MATCH (a:Node {id: anchorId})
SET a.role = 'anchor'
WITH a, followers
UNWIND followers AS fid
WITH a, toInteger(fid) AS fid WHERE fid IS NOT NULL
MATCH (f:Node {id: fid})
SET f.role = 'follower'
MERGE (a)-[:FOLLOW]->(f);



-----------------
//验证跟随者和节点直接的关系
MATCH (a:Node {role: 'anchor'})-[r:FOLLOW]->(f:Node {role: 'follower'})
RETURN a.id AS anchor, collect(f.id) AS followers;



//添加颜色
// 基础唯一约束（建议）
CREATE CONSTRAINT node_id IF NOT EXISTS FOR (n:Node) REQUIRE n.id IS UNIQUE;

// 根据已有属性加标签
MATCH (a:Node) WHERE a.role = 'anchor'    SET a:Anchor;
MATCH (f:Node) WHERE f.role = 'follower'  SET f:Follower;
MATCH (n:Node) WHERE "1_4" IN n.kl        SET n:KL_1_4;

:style reset




//显示查询结果
MATCH (n:Node)-[r:LINK]->(m:Node) WHERE "1_4" IN n.kl AND "1_4" IN m.kl RETURN n, r, m;

MATCH (n:Node)-[r:LINK]->(m:Node) WHERE "1_4" IN n.kl AND "1_4" IN m.kl OR ((n.role = 'anchor' OR n.role = 'follower') or (m.role = 'anchor' OR m.role = 'follower')) RETURN n, r, m;
-------------------

//删除图信息
MATCH (n) DETACH DELETE n;


//删除投影

CALL gds.graph.drop('testGraph') YIELD graphName;

